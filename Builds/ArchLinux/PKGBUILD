# Maintainer: Roberto Catini <roberto.catini@gmail.com>

pkgname=stoxumd
pkgrel=1
pkgver=0
pkgdesc="Stoxum peer-to-peer network daemon"
arch=('i686' 'x86_64')
url="https://github.com/stoxum/stoxumd"
license=('custom:ISC')
depends=('protobuf' 'openssl' 'boost-libs')
makedepends=('git' 'scons' 'boost')
backup=("etc/$pkgname/stoxumd.cfg")
source=("git://github.com/stoxum/stoxumd.git#branch=master")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	scons
}

check() {
	cd "$srcdir/$pkgname"
	build/stoxumd --unittest
}

package() {
	cd "$srcdir/$pkgname"
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D build/stoxumd "$pkgdir/usr/bin/stoxumd"
	install -D -m644 doc/stoxumd-example.cfg "$pkgdir/etc/$pkgname/stoxumd.cfg"
	mkdir -p "$pkgdir/var/lib/$pkgname/db"
	mkdir -p "$pkgdir/var/log/$pkgname"
}
